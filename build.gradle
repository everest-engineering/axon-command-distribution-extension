plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.sonarqube' version '3.3'
    id 'com.diffplug.spotless' version '6.0.0'
    id 'jacoco'
}

group = 'engineering.everest.axon'

def gitTagVersion = versionDetails()
version = gitTagVersion.commitDistance == 0
        ? gitTagVersion.lastTag
        : "${gitTagVersion.lastTag}+${gitTagVersion.commitDistance}-SNAPSHOT"

ext {
    axonVersion = '4.5.5'
    ehCacheVersion = '3.9.7'
    hazelcastKubernetesVersion = '2.2.3'
    hazelcastVersion = '4.2.2'
    javaXCacheVersion = '1.1.1'
    kubernetesClientVersion = '10.0.1'
    lombokVersion = '1.18.22'
    springBootVersion = '2.6.0'

    junitVersion = '5.8.1'
    mockitoVersion = '4.0.0'
    systemLambdaVersion = '1.2.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-actuator'
    api "com.hazelcast:hazelcast:${hazelcastVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "javax.cache:cache-api:${javaXCacheVersion}"
    implementation "org.ehcache:ehcache:${ehCacheVersion}"
    implementation "org.axonframework:axon-modelling:${axonVersion}"
    implementation "com.hazelcast:hazelcast-kubernetes:${hazelcastKubernetesVersion}"
    implementation "io.kubernetes:client-java:${kubernetesClientVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.springframework:spring-test'
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "com.github.stefanbirkner:system-lambda:${systemLambdaVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    java {
        withJavadocJar()
        withSourcesJar()
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        eclipse().configFile("$rootDir/eclipse-formatter-config.xml")
        indentWithSpaces(4)
        trimTrailingWhitespace()
        removeUnusedImports()
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

apply from: 'publishing.gradle'
apply from: 'sonar.gradle'
