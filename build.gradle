plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'idea'
    id 'io.freefair.lombok' version '5.1.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java-library'
    id 'maven-publish'
    id 'org.sonarqube' version '3.1.1'
    id 'jacoco'
}

apply plugin: 'io.freefair.lombok'

group = 'engineering.everest.starterkit'
sourceCompatibility = '1.11'
version gitVersion()

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

lombok {
    version = '1.18.16'
    generateLombokConfig.enabled = false
    config['lombok.addLombokGeneratedAnnotation'] = 'true'
}

ext {
    axonVersion = '4.4.5'
    ehCacheVersion = '3.9.0'
    hazelcastKubernetesVersion = '2.0.2'
    hazelcastVersion = '4.0.3'
    javaXCacheVersion = '1.1.1'
    kubernetesClientVersion = '10.0.0'
    springBootVersion = '2.4.0'

    junitVersion = '5.7.0'
    mockitoVersion = '3.5.15'
    systemLambdaVersion = '1.1.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-actuator'
    api "com.hazelcast:hazelcast:${hazelcastVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "javax.cache:cache-api:${javaXCacheVersion}"
    implementation "org.ehcache:ehcache:${ehCacheVersion}"
    implementation "org.axonframework:axon-modelling:${axonVersion}"
    implementation "com.hazelcast:hazelcast-kubernetes:${hazelcastKubernetesVersion}"
    implementation "io.kubernetes:client-java:${kubernetesClientVersion}"

    testImplementation 'org.springframework:spring-test'
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "com.github.stefanbirkner:system-lambda:${systemLambdaVersion}"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'starterkit'
        name = rootProject.name
        userOrg = 'everestengineering'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/everestengineering/starterkit-axon-command-distribution-extension'
    }
    publish = true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

test.finalizedBy jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "Axon command distribution extension"
        property "sonar.projectKey", "everest-engineering_axon-command-distribution-extension"
        property "sonar.organization", "everestengineering"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.test.exclusions', '**/*Test.java'
    }
}

tasks['sonarqube'].dependsOn test
