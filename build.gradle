plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'idea'
    id 'io.freefair.lombok' version '5.0.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'io.freefair.lombok'

group = 'engineering.everest.starterkit'
sourceCompatibility = '1.11'
version gitVersion()

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

lombok {
    version = '1.18.12'
    generateLombokConfig.enabled = false
}

ext {
    axonVersion = '4.3.3'
    ehCacheVersion = '3.8.1'
    hazelcastKubernetesVersion = '1.5.3'
    hazelcastVersion = '3.12.7'
    javaXCacheVersion = '1.1.1'
    kubernetesClientVersion = '6.0.1'
    springBootVersion = '2.2.7.RELEASE'

    junitVersion = '5.6.2'
    mockitoVersion = '3.3.3'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-actuator'
    api "com.hazelcast:hazelcast:${hazelcastVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "javax.cache:cache-api:${javaXCacheVersion}"
    implementation "org.ehcache:ehcache:${ehCacheVersion}"
    implementation "org.axonframework:axon-modelling:${axonVersion}"
    implementation "com.hazelcast:hazelcast-kubernetes:${hazelcastKubernetesVersion}"
    implementation "io.kubernetes:client-java:${kubernetesClientVersion}"

    testImplementation 'org.springframework:spring-test'
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    pkg {
        repo = 'starterkit'
        name = rootProject.name
        userOrg = 'everestengineering'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/everestengineering/starterkit-axon-command-distribution-extension'
    }
    publish = true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
